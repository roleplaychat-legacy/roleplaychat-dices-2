buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = modVersion
group = modGroup
archivesBaseName = "$modBaseName-forge-1.12.2"

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"

    replaceIn "RoleplayChatDices.java"
    replace "@@MODID@@", modBaseName
    replace "@@VERSION@@", project.version

    replaceIn "mcmod.info"
    replace "@@MODID@@", modBaseName
    replace "@@VERSION@@", project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade project(":roleplay_chat_dices-common")
}
/*
    From https://github.com/CyclopsMC/IntegratedDynamics/blob/master-1.12/gradle/forge.gradle#L50
    A fix for the build-time warning (which for some reason only occurs on Travis):
    :extractAnnotationsJarError while building FML annotations cache: duplicate entry: META-INF/fml_cache_annotation.json
    java.util.zip.ZipException: duplicate entry: META-INF/fml_cache_annotation.json
    FML has a fallback to derive this annotation information at runtime, so this is not a major problem.
*/
extractAnnotationsJar.enabled = false

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}